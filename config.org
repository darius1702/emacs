#+TITLE: Emacs configuration
#+AUTHOR: Darius Schefer
#+PROPERTY: header-args:emacs-lisp :tangle init.el :mkdirp yes
#+STARTUP: show2levels

* Early init
** No premature redisplay
Prevent some flickering on startup
(Doesn't really prevent any flickering)

#+begin_src emacs-lisp :tangle early-init.el
;; -*- lexical-binding: t -*-
(setq-default inhibit-redisplay t
              inhibit-message t)
(add-hook 'window-setup-hook
          (lambda ()
            (setq-default inhibit-redisplay nil
                          inhibit-message nil)
            (redisplay)))
#+end_src

** Faster startup
Change some variables until after startup to make emacs load faster

#+begin_src emacs-lisp :tangle early-init.el
(setq gc-cons-threshold (* 1000 1000 1000))

(defvar darius/file-name-handler-alist file-name-handler-alist)
(setq file-name-handler-alist nil)

(defvar darius/vc-handled-backends vc-handled-backends)
(setq vc-handled-backends nil)

(add-hook 'emacs-startup-hook
          (lambda ()
            (setq file-name-handler-alist darius/file-name-handler-alist
                  vc-handled-backends darius/vc-handled-backends)))
#+end_src

** Default clutter
Adjust some default settings

#+begin_src emacs-lisp :tangle early-init.el
;; please work
(setq evil-want-keybinding nil)
(setq evil-want-C-u-scroll t)

(setq inhibit-startup-message t)
(setq native-comp-async-report-warnings-errors nil)
(setq initial-scratch-message "")
(setq server-client-instructions nil)
(tool-bar-mode 0)
(menu-bar-mode 0)
(scroll-bar-mode 0)
(tooltip-mode 0)
(blink-cursor-mode 0)
(setq frame-resize-pixelwise t)
(setq use-dialog-box nil)
(setq use-short-answers t)
#+end_src

* Basic stuff
** Annoying files
Disble all annoying auto-generated files and make ~custom-set-variables~ go to their own file

#+begin_src emacs-lisp
;; -*- lexical-binding: t -*-
(make-directory (expand-file-name "tmp/auto-saves/" user-emacs-directory) t)
(setq auto-save-list-file-prefix (expand-file-name "tmp/auto-saves/sessions/" user-emacs-directory)
      auto-save-file-name-transforms `((".*" ,(expand-file-name "tmp/auto-saves/" user-emacs-directory) t)))
(setq backup-directory-alist `(("." . ,(expand-file-name "tmp/backups/" user-emacs-directory))))
(setq backup-by-copying t)

;; Enable when lockfiles become annoying
;; (setq create-lockfiles nil)

(setq custom-file (concat user-emacs-directory "custom.el"))
(when (file-exists-p custom-file)
  (load custom-file))
#+end_src

** Better editing experience
Some sensible defaults

#+begin_src emacs-lisp
(global-set-key (kbd "<escape>") 'keyboard-escape-quit)
(global-unset-key (kbd "C-x C-c"))      ; stop accidentally quitting emacs

(setq read-buffer-completion-ignore-case t)
(setq scroll-conservatively 101)
(setq scroll-margin 3)
(setq display-line-numbers-type 'relative)
(setq-default indent-tabs-mode nil)
(column-number-mode t)
(show-paren-mode t)
(global-visual-line-mode t)
(electric-pair-mode t)
(add-to-list 'write-file-functions 'delete-trailing-whitespace)

(let ((cargo-path (expand-file-name "~/.cargo/bin")))
  (setenv "PATH" (concat cargo-path ":" (getenv "PATH")))
  (add-to-list 'exec-path cargo-path))
#+end_src

** Prog-mode setup
Make programming-modes a little nicer with line numbers and current line highlighting

#+begin_src emacs-lisp
(add-hook 'prog-mode-hook 'display-line-numbers-mode)
(add-hook 'prog-mode-hook 'hl-line-mode)
#+end_src

** Man pages
Make manpages look a little better

#+begin_src emacs-lisp
(require 'man)
(set-face-attribute 'Man-overstrike nil :inherit font-lock-type-face :bold t)
(set-face-attribute 'Man-underline nil :inherit font-lock-keyword-face :underline t)
#+end_src

** Ibuffer
Buffer switching and management

#+begin_src emacs-lisp
(global-set-key (kbd "C-x C-b") 'ibuffer)
(setq ibuffer-expert t)
(setq ibuffer-display-summary nil)
(setq ibuffer-saved-filter-groups
      (quote (("default"
               ("Code" (or (derived-mode . prog-mode) (mode . ess-mode)
                           (mode . compilation-mode)))
               ("LaTeX" (mode . latex-mode))
               ("Dired" (mode . dired-mode))
               ("Org" (mode . org-mode))
               ("Help" (or (mode . help-mode) (mode . Man-mode)))
               ("Git" (name . "^magit:"))
               ("Misc" (name . "^\\**.*\\*$"))))))

(setq ibuffer-formats
      '((mark modified read-only vc-status-mini " "
              (name 18 18 :left :elide)
              " "
              (size 9 -1 :right)
              " "
              (mode 16 16 :left :elide)
              " "
              (vc-status 16 16 :left))))

(add-hook 'ibuffer-mode-hook
          (lambda ()
            (ibuffer-switch-to-saved-filter-groups "default")
            (ibuffer-auto-mode t)))
#+end_src

** Dired
Make dired recognize other buffers as copy/move targets and also list human-readable filesizes

#+begin_src emacs-lisp
(setq dired-dwim-target 1)
(setq dired-listing-switches "-alh"):config
#+end_src

** Default Applications
Default programs for opening filetypes
This is probably he wrong way to do it?

#+begin_src emacs-lisp
(setq org-file-apps '((auto-mode . emacs) (directory . emacs) ("\\.mm\\'" . default) ("\\.x?html?\\'" . default)))
#+end_src

** Font setup
Iosevka is the best font fight me.
Need to set it in an extra hook to make it work in emacsclient frames.

#+begin_src emacs-lisp
(setq darius/fixed-pitch-font "Iosevka Nerd Font Mono")
(setq darius/variable-pitch-font "Iosevka Aile")

(defun darius/set-up-fonts ()
  (set-face-attribute 'default nil :font darius/fixed-pitch-font :height 150)
  (set-face-attribute 'variable-pitch nil :font  darius/variable-pitch-font :weight 'regular)
  (set-face-attribute 'fixed-pitch nil :font darius/fixed-pitch-font))

(add-hook 'after-init-hook 'darius/set-up-fonts)
(add-hook 'server-after-make-frame-hook 'darius/set-up-fonts)
#+end_src

* Packages
** Setup
Basic ~package.el~ config

#+begin_src emacs-lisp
(require 'package)
(add-to-list 'package-archives '("melpa" . "https://melpa.org/packages/") t)
(package-initialize)
(unless package-archive-contents
  (package-refresh-contents))
(unless (package-installed-p 'use-package)
  (package-install 'use-package))
(require 'use-package)
(setq use-package-always-ensure t)
(setq package-native-compile t)         ; this will just be ignored if native-comp isn't available
#+end_src

** Useful random stuff
Some packages that don't require much configuration

*** Ibuffer-vc
Version control integration for Ibuffer

#+begin_src emacs-lisp
(use-package ibuffer-vc)
#+end_src

*** Dired open
Open files in custom programs in dired

#+begin_src emacs-lisp
(use-package dired-open
  :config
  (setq dired-open-extensions '(("jpg" . "sxiv")
                                ("png" . "sxiv")
                                ("mkv" . "mpv")
                                ("mp4" . "mpv"))))
#+end_src

*** Marginalia
Usful info in the minibuffer

#+begin_src emacs-lisp
(use-package marginalia
  :init (marginalia-mode))
#+end_src

*** Rainbow-mode
Colorize strings like #a7c080

#+begin_src emacs-lisp
(use-package rainbow-mode
  :config (rainbow-mode)
  :diminish rainbow-mode)
#+end_src

*** Consult
Some nice additional completing-read stuff

#+begin_src emacs-lisp
(use-package consult)
#+end_src

*** Which-key

#+begin_src emacs-lisp
(use-package which-key
  :init (which-key-mode)
  :diminish which-key-mode)
#+end_src

*** Expand-region

#+begin_src emacs-lisp
(use-package expand-region
  :bind (("M-[" . er/expand-region)
         ("C-(" . er/mark-outside-pairs)))
#+end_src

** PDF Tools
Some improvements over DocView

#+begin_src emacs-lisp
(use-package pdf-tools
  :config
  (pdf-tools-install)
  (setq-default pdf-view-display-size 'fit-page)
  (add-to-list 'revert-without-query ".pdf")
  :bind (:map pdf-view-mode-map
              ("C-=" . pdf-view-enlarge)
              ("C--" . pdf-view-shrink)
              ("g"  . pdf-view-first-page)
              ("G"  . pdf-view-last-page)
              ("l"  . image-forward-hscroll)
              ("h"  . image-backward-hscroll)
              ("j"  . pdf-view-next-line-or-next-page)
              ("k"  . pdf-view-previous-line-or-previous-page)
              ("e"  . pdf-view-goto-page)
              ("i"  . pdf-misc-display-metadata)
              ("s"  . pdf-occur)))

(add-hook 'pdf-view-mode-hook #'(lambda () (interactive) (display-line-numbers-mode -1)))
(add-hook 'doc-view-mode-hook #'(lambda () (progn
                                             (pdf-tools-install)
                                             (pdf-view-mode))))
#+end_src

** Git
Some git tools

*** Magit
Very nice git interface

#+begin_src emacs-lisp
(use-package magit)
#+end_src

*** Git-Gutter
Git status in the gutter

#+begin_src emacs-lisp
(use-package git-gutter
  :diminish
  :init
  (setq
   git-gutter:update-interval 0
   git-gutter:modified-sign "│"
   git-gutter:added-sign "│"
   git-gutter:deleted-sign "│")
  :config
  (set-face-foreground 'git-gutter:modified "#7fbbb3")
  :hook (prog-mode . git-gutter-mode))
#+end_src

** Evil
Not really a fan but text editing is even worse without it.
I have no idea what needs to go in ~:init~ and what in ~:config~

#+begin_src emacs-lisp
(use-package evil
  :init
  (evil-mode 1)
  :config
  (evil-set-undo-system 'undo-redo)
  (setq evil-mode-line-format nil))	; no <N> indicator in modeline

;; Make RET still follow links in org mode
(with-eval-after-load 'evil-maps
  (define-key evil-motion-state-map (kbd "RET") nil))

(use-package evil-collection
  :after evil
  :config (evil-collection-init))
#+end_src

** General
Setup keybindings

#+begin_src emacs-lisp
(use-package general
  :config (general-evil-setup)

  (general-create-definer darius/leader-keys
    :states '(normal visual)
    :keymaps 'override
    :prefix "SPC")

  (darius/leader-keys
   "SPC" '(switch-to-buffer             :wk "Switch to buffer")
   "/"   '(consult-line                 :wk "Consult line")
   "d"   '(dired                        :wk "Dired")
   "D"   '(dired-other-window           :wk "Dired other window")
   "p"   '(consult-yank-from-kill-ring  :wk "Paste from history")
   "u"   '(universal-argument           :wk "Universal argument")
   "x"   '(execute-extended-command     :wk "M-x"))

  (darius/leader-keys
   "b"   '(:ignore t                    :wk "Buffer")
   "b b" '(ibuffer                      :wk "Ibuffer")
   "b f" '(consult-buffer               :wk "Find")
   "b k" '(kill-buffer                  :wk "Kill")
   "b w" '(widen                        :wk "Widen"))

  (darius/leader-keys
   "c"   '(:ignore t                    :wk "Compiler")
   "c c" '(compile                      :wk "Compile")
   "c e" '(consult-compile-error        :wk "Goto error")
   "c r" '(recompile                    :wk "Recompile"))

  (darius/leader-keys
   "f"   '(:ignore t                    :wk "Find")
   "f d" '(consult-fd                   :wk "Consult fd")
   "f f" '(find-file                    :wk "File")
   "f F" '(find-file-other-window       :wk "File other window")
   "f g" '(consult-ripgrep              :wk "Grep")
   "f i" '(consult-imenu-multi          :wk "Imenu")
   "f m" '(man                          :wk "Manpage"))

  (darius/leader-keys
   "g"   '(:ignore t                    :wk "Git")
   "g b" '(magit-blame                  :wk "Blame")
   "g n" '(git-gutter:next-hunk         :wk "Next hunk")
   "g p" '(git-gutter:previous-hunk     :wk "Previous hunk")
   "g s" '(magit-status                 :wk "Status")
   "g v" '(git-gutter:popup-hunk        :wk "View hunk"))

  (darius/leader-keys
   "h"   '(:ignore t                    :wk "Help")
   "h f" '(describe-function            :wk "Function")
   "h k" '(describe-key                 :wk "Key")
   "h v" '(describe-variable            :wk "Variable"))

  (darius/leader-keys
   "l"   '(:ignore t                    :wk "LaTeX")
   "l e" '(LaTeX-environment            :wk "Environment")
   "l m" '(TeX-insert-macro             :wk "Macro")
   "l s" '(LaTeX-section                :wk "Section"))

  (darius/leader-keys
   "o"   '(:ignore t                    :wk "Org")
   "o a" '(org-agenda                   :wk "Agenda")
   "o c" '(org-ctrl-c-ctrl-c            :wk "C-c C-c")
   "o d" '(org-deadline                 :wk "Deadline")
   "o e" '(org-edit-special             :wk "Edit")
   "o f" '(consult-org-agenda           :wk "Find in Agenda")
   "o h" '(consult-org-heading          :wk "Heading")

   "o l"   '(:ignore t                  :wk "Link")
   "o l i" '(org-insert-link            :wk "Insert")
   "o l y" '(org-store-link             :wk "Store")

   "o n"   '(:ignore t                  :wk "Narrow")
   "o n s" '(org-narrow-to-subtree      :wk "Subtree")

   "o p" '(org-priority                 :wk "Priority")
   "o s" '(org-schedule                 :wk "Schedule")
   "o t" '(org-todo                     :wk "Todo")
   "o y" '(org-store-link               :wk "Copy link"))

  (darius/leader-keys
   "w"   '(:ignore t                    :wk "Window")
   "w o" '(delete-other-windows         :wk "Delete others")
   "w q" '(delete-window                :wk "Quit")
   "w s" '(split-window-below           :wk "H-Split")
   "w v" '(split-window-right           :wk "V-Split"))

  (darius/leader-keys
   "z"   '(:ignore t                    :wk "Citation")
   "z d" '(citar-dwim                   :wk "Dwim")
   "z i" '(citar-insert-citation        :wk "Insert")
   "z o" '(citar-open                   :wk "Open")))
#+end_src

** Org
Some org-mode tweaks

#+begin_src emacs-lisp
(use-package org-modern
:config (global-org-modern-mode))

(defun darius/org-setup ()
  (setq org-directory "~/Notes")
  (setq org-default-notes-file (concat org-directory "/scratch.org"))
  (setq org-agenda-files '("~/Notes"))
  (setq org-todo-keywords '((sequence "TODO" "IN-PROGRESS" "WAITING" "DONE")))
  (setq org-return-follows-link t))

(defun darius/org-font-setup ()
  (custom-set-faces '(org-document-title ((t (:height 1.3)))))
  ;; Larger font size for some headings
  (dolist (face '((org-level-1 . 1.15)
                  (org-level-2 . 1.1)
                  (org-level-3 . 1.05)
                  (org-level-4 . 1.0)
                  (org-level-5 . 1.0)
                  (org-level-6 . 1.0)
                  (org-level-7 . 1.0)
                  (org-level-8 . 1.0)))
    (set-face-attribute (car face) nil :font darius/fixed-pitch-font :weight 'regular :height (cdr face))))

(use-package org
  :config
  (darius/org-setup)
  (darius/org-font-setup)
  (setq org-src-preserve-indentation nil
        org-edit-src-content-indentation 0)
  (setq org-ellipsis "▾"))

(add-hook 'org-mode-hook 'org-indent-mode)

;; Fix weird internal link behavior
(with-eval-after-load 'org-ctags (setq org-open-link-functions nil))
#+end_src

** Completion at point
Corfu for in-buffer completion

#+begin_src emacs-lisp
(use-package corfu
  :custom
  (corfu-cycle t)
  (corfu-auto t)
  (corfu-auto-prefix 0)
  (corfu-auto-delay 0)
  (corfu-separator ?\s)             ;; Orderless field separator
  ;; (corfu-quit-at-boundary nil)   ;; Never quit at completion boundary
  ;; (corfu-quit-no-match nil)      ;; Never quit, even if there is no match
  ;; (corfu-preview-current nil)    ;; Disable current candidate preview
  ;; (corfu-preselect 'prompt)      ;; Preselect the prompt
  ;; (corfu-on-exact-match nil)     ;; Configure handling of exact matches
  ;; (corfu-scroll-margin 5)        ;; Use scroll margin

  :bind
  (:map corfu-map
        ("RET" . nil))

  :init (global-corfu-mode))

;; A few more useful configurations...
(use-package emacs
  :init
  ;; TAB cycle if there are only few candidates
  (setq completion-cycle-threshold 3)

  ;; Emacs 28: Hide commands in M-x which do not apply to the current mode.
  ;; Corfu commands are hidden, since they are not supposed to be used via M-x.
  ;; (setq read-extended-command-predicate
  ;;       #'command-completion-default-include-p)

  ;; Enable indentation+completion using the TAB key.
  ;; `completion-at-point' is often bound to M-TAB.
  (setq tab-always-indent 'complete))
#+end_src

** Minibuffer completion
Set up vertico, orderless and savehist and tweak some emacs completion defaults

#+begin_src emacs-lisp
(use-package vertico
  :init (vertico-mode))

(use-package orderless
  :init
  ;; Configure a custom style dispatcher (see the Consult wiki)
  ;; (setq orderless-style-dispatchers '(+orderless-consult-dispatch orderless-affix-dispatch)
  ;;       orderless-component-separator #'orderless-escapable-split-on-space)
  (setq completion-styles '(substring orderless basic)
	completion-category-defaults nil
	completion-category-overrides '((file (styles partial-completion)))))

(use-package emacs
  :init
  ;; Add prompt indicator to `completing-read-multiple'.
  ;; We display [CRM<separator>], e.g., [CRM,] if the separator is a comma.
  (defun crm-indicator (args)
    (cons (format "[CRM%s] %s"
		  (replace-regexp-in-string
		   "\\`\\[.*?]\\*\\|\\[.*?]\\*\\'" ""
		   crm-separator)
		  (car args))
	  (cdr args)))
  (advice-add #'completing-read-multiple :filter-args #'crm-indicator)

  ;; Do not allow the cursor in the minibuffer prompt
  (setq minibuffer-prompt-properties
	'(read-only t cursor-intangible t face minibuffer-prompt))
  (add-hook 'minibuffer-setup-hook #'cursor-intangible-mode)

  ;; Emacs 28: Hide commands in M-x which do not work in the current mode.
  ;; Vertico commands are hidden in normal buffers.
  ;; (setq read-extended-command-predicate
  ;;       #'command-completion-default-include-p)

  ;; Enable recursive minibuffers
  (setq enable-recursive-minibuffers t))

(use-package savehist
  :init (savehist-mode))
#+end_src

** Colorscheme
The most important thing tbh.
doom-everforest theme depends on the ~doom-themes~ package
- [ ] Get rid of ~doom-themes~ dependency

#+begin_src emacs-lisp
(add-to-list 'custom-theme-load-path "~/.emacs.d/doom-everforest-theme")
(setq doom-everforest-background "hard")
(use-package doom-themes
:config
(doom-themes-org-config)
(setq doom-themes-enable-italic nil))

(defun darius/set-buffer-name-color (window)
  "Color the filename in the currently selected buffer based on whether it's modified and dim it in inactive buffers"
  (with-current-buffer (window-buffer window)
    (if (not (eq (current-buffer) (window-buffer (selected-window))))
        (face-remap-set-base 'mode-line-buffer-id '(:foreground "#414b50"))
      (if (buffer-modified-p)
        (face-remap-set-base 'mode-line-buffer-id '(:foreground "#e67e80"))
        (face-remap-set-base 'mode-line-buffer-id '(:foreground "#a7c080"))))))

(add-hook 'post-command-hook (lambda () (walk-windows #'darius/set-buffer-name-color nil t)))

(load-theme 'doom-everforest t)
#+end_src

** Diminish
Get rid of some clutter in the modeline
Doesn't work properly if it's not all the way at the end for some reason

#+begin_src emacs-lisp
(use-package diminish
  :diminish visual-line-mode
  :diminish auto-revert-mode
  :diminish evil-collection-unimpaired-mode)
#+end_src

** Windows and Frames
Switching and moving windows

#+begin_src emacs-lisp
(use-package ace-window
  :bind (("M-o" . ace-window) ("M-O" . ace-swap-window))
  :custom
  (aw-scope 'frame))

(use-package transpose-frame
  :bind ("C-M-o" . transpose-frame))

(global-set-key (kbd "M-H") 'windmove-left)
(global-set-key (kbd "M-J") 'windmove-down)
(global-set-key (kbd "M-K") 'windmove-up)
(global-set-key (kbd "M-L") 'windmove-right)
#+end_src

** Spacious Padding
Give windows some breathing room

#+begin_src emacs-lisp
(use-package spacious-padding
  :custom
  (spacious-padding-subtle-mode-line
   '(:mode-line-active "#a7c080" :mode-line-inactive "#373737"))
  (spacious-padding-widths '(:internal-border-width 3 :header-line-width 4 :mode-line-width 1 :right-divider-width 10))
  :config
  (spacious-padding-mode 1))
#+end_src

* Languages
Programming language specific stuff

** Eglot Setup
Language server stuff

#+begin_src emacs-lisp
(use-package eglot
  :config
  (customize-set-variable 'eglot-autoshutdown t)
  (customize-set-variable 'eglot-extend-to-xref t)
  (add-to-list 'eglot-server-programs
               '((c-mode c++-mode)
                 . ("clangd"
                    "-j=8"
                    "--log=error"
                    "--malloc-trim"
                    "--background-index"
                    "--clang-tidy"
                    "--cross-file-rename"
                    "--completion-style=detailed"
                    "--pch-storage=memory"
                    "--header-insertion=never"
                    "--header-insertion-decorators=0")))
  (add-hook 'c-mode-hook #'eglot-ensure)
  (add-hook 'c++-mode-hook #'eglot-ensure)
  (add-hook 'latex-mode-hook #'eglot-ensure))
#+end_src

** Rust
Funny orange crab

#+begin_src emacs-lisp
(use-package rustic
  :config
  (setq rustic-cargo-bin-remote "/usr/local/cargo/bin/cargo")
  (setq rustic-lsp-client 'eglot))
#+end_src

** Haskell
The one and only

#+begin_src emacs-lisp
(use-package haskell-mode
  :init
  (setq flymake-allowed-file-name-masks '())
  :config
  (let ((my-ghcup-path (expand-file-name "~/.ghcup/bin")))
    (setenv "PATH" (concat my-ghcup-path ":" (getenv "PATH")))
    (add-to-list 'exec-path my-ghcup-path))
  (let ((my-cabal-path (expand-file-name "~/.cabal/bin")))
    (setenv "PATH" (concat my-cabal-path ":" (getenv "PATH")))
    (add-to-list 'exec-path my-cabal-path))
  :bind (:map haskell-mode-map
              ("M-n" . 'haskell-goto-next-error)
              ("M-p" . 'haskell-goto-prev-error)))
#+end_src

** LaTeX and Citar
Work with citations
Also requires auctex
What the hecc is a reftex

#+begin_src emacs-lisp
(setq TeX-parse-self t)
(setq-default TeX-master nil)
(setq LaTeX-electric-left-right-brace t)
(setq reftex-plug-into-AUCTeX t)

(use-package tex
  :ensure auctex)

(use-package citar
  :custom
  (setq citar-file-open-functions '(("html" . citar-file-open-external) ("pdf" . citar-file-open-external) (t . find-file)))
  (citar-bibliography '("~/Documents/library.bib")))

(use-package cdlatex
  :config
  (add-hook 'LaTeX-mode-hook #'turn-on-cdlatex))
#+end_src

* Custom Functions
Various cringe

** COMMENT Old EXWM Stuff
Used to be stuff for EXWM but kept around just in case

#+begin_src emacs-lisp
(defun darius/get_executables_in_path ()
  (split-string (shell-command-to-string "dmenu_path") "\n"))

(defun darius/run ()
  (interactive)
  (let* ((option (completing-read "Run: " (darius/get_executables_in_path))))
    (start-process option nil option))) ;; re-use option for process name as well

(defun darius/exwm-update-class ()
  (exwm-workspace-rename-buffer exwm-class-name))

(defun darius/exwm-update-title ()
  (pcase exwm-class-name
    ("firefox" (exwm-workspace-rename-buffer (format "Firefox: %s" exwm-title)))))

(defun darius/run-in-background (command)
  (let ((command-parts (split-string command "[ ]+")))
    (apply #'call-process `(,(car command-parts) nil 0 nil ,@(cdr command-parts)))))

(defun darius/set-volume (amount)
  "Set the system volume to the AMOUNT string using pactl"
  (let ((command (format "pactl -- set-sink-volume @DEFAULT_SINK@ %s" amount)))
    (start-process-shell-command command nil command)))

(defun darius/volume-up-percent (amount)
  "Increase system volume by AMOUNT percent"
  (darius/set-volume (format "+%d%%" amount)))

(defun darius/volume-down-percent (amount)
  "Decrease system volume by AMOUNT percent"
  (darius/set-volume (format "-%d%%" amount)))

(defun darius/volume-mute ()
  "Mute system volume using pactl"
  (let ((command "pactl -- set-sink-mute @DEFAULT_SINK@ toggle"))
    (start-process-shell-command command nil command)))

(defun darius/mic-mute ()
  "Mute the microphone using pactl"
  (let ((command "pactl -- set-source-mute 0 toggle"))
    (start-process-shell-command command nil command)))

(defun darius/set-brightness (amount)
  "Pass AMOUNT string to brightnessctl"
  (let ((command (format "brightnessctl s %s" amount)))
    (start-process-shell-command command nil command)))

(defun darius/brightness-up (amount)
  "Increase screen brightness by AMOUNT"
  (darius/set-brightness (format "%d+" amount)))

(defun darius/brightness-down (amount)
  "Decrease screen brightness by AMOUNT"
  (darius/set-brightness (format "%d-" amount)))

(defun darius/lock-screen ()
  "Lock the screen using i3lock"
  (start-process-shell-command "~/.config/i3/lock.sh" nil "~/.config/i3/lock.sh"))

(defun darius/trackpad-toggle ()
  "Disable the trackpad using xinput"
  (start-process-shell-command "~/Dotfiles/scripts/toggle_trackpad.sh" nil "~/Dotfiles/scripts/toggle_trackpad.sh"))
#+end_src
